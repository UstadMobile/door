plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'kotlinx-atomicfu'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
}

group rootProject.group
version rootProject.version

repositories {
    mavenCentral()
}


android {
    compileSdk rootProject.ext.version_android_compile_sdk
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    namespace 'com.ustadmobile.door.runtime'
}

kotlin {
    android {
        publishLibraryVariants("release", "debug")
    }

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }

    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
    }

    js(IR) {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    /* This is not supported from Kotlin 1.8
                     * webpackConfig.cssSupport.enabled = true
                     */
                }
            }
        }
    }
    /* Targets configuration omitted. 
    *  To find out how to configure the targets, please follow the link:
    *  https://kotlinlang.org/docs/reference/building-mpp-with-gradle.html#setting-up-targets */

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":room-annotations")
                implementation kotlin("stdlib-common")
                implementation "io.ktor:ktor-client-core:$version_ktor"
                implementation "io.ktor:ktor-client-json:$version_ktor"
                implementation "io.github.aakira:napier:$version_napier"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$version_kotlinx_serialization"
                implementation "org.kodein.di:kodein-di:$version_kodein_di"
                implementation "org.jetbrains.kotlinx:atomicfu:$version_atomicfu"
            }
        }

        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }

        roomAnnotations {

        }

        commonJvmMain {
            dependencies {
                compileOnly "org.nanohttpd:nanohttpd:$version_nanohttpd"
                compileOnly "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"
            }
        }

        androidMain {
            dependsOn commonJvmMain
            dependencies {
                implementation project(":room-annotations")
                implementation "androidx.room:room-runtime:$version_android_room"
                implementation "androidx.room:room-ktx:$version_android_room"
                implementation "androidx.lifecycle:lifecycle-livedata-ktx:$version_androidx_lifecycle"
                implementation "androidx.paging:paging-runtime:$version_androidx_paging"
                implementation "androidx.core:core-ktx:$version_androidx_core"
                implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
                implementation "com.squareup.okhttp3:okhttp-sse:$version_okhttp"
            }
        }

        androidTest {

        }

        commonJvmJs {
            dependsOn commonMain

            dependencies {
                implementation kotlin("stdlib-common")
            }
        }


        jvmMain {
            dependsOn roomAnnotations
            dependsOn commonJvmMain
            dependsOn commonJvmJs

            dependencies {
                implementation "org.kodein.di:kodein-di-framework-ktor-server-jvm:$version_kodein_di"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "org.postgresql:postgresql:$version_postgres_jdbc"
                implementation "io.ktor:ktor-server-core:$version_ktor"
                implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
                implementation "com.squareup.okhttp3:okhttp-sse:$version_okhttp"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$version_kotlin"
                implementation "com.zaxxer:HikariCP:$version_hikaricp"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test-junit")

                implementation "org.mockito.kotlin:mockito-kotlin:$version_kotlin_mockito"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "com.squareup.okhttp3:mockwebserver:$version_mockwebserver"

                implementation "io.ktor:ktor-server:$version_ktor"
                implementation "io.ktor:ktor-client-core:$version_ktor"
                implementation "io.ktor:ktor-server-core:$version_ktor"
                implementation "io.ktor:ktor-server-netty:$version_ktor"
                implementation "io.ktor:ktor-server-content-negotiation:$version_ktor"
                implementation "io.ktor:ktor-client-content-negotiation:$version_ktor"
                implementation "io.ktor:ktor-serialization-gson:$version_ktor"
                implementation "io.ktor:ktor-client-okhttp:$version_ktor"
                implementation "io.ktor:ktor-client-gson:$version_ktor"
                implementation "io.ktor:ktor-server-test-host:$version_ktor"
            }
        }
        jsMain {
            dependsOn roomAnnotations
            dependsOn commonJvmJs

            dependencies {
                implementation "org.jetbrains.kotlin-wrappers:kotlin-extensions:$version_kotlin_extensions"
                implementation npm("spark-md5","$version_npm_sparkmd5")
            }
        }
        jsTest {
            dependencies {
                implementation kotlin("test-js")
            }
        }
    }
}

publishing {
    repositories {
        maven {
            url rootProject.ext.localProperties["repo.dir"]
        }
    }
}