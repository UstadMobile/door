plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'kotlinx-atomicfu'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

group rootProject.group
version rootProject.version

repositories {
    jcenter()
    mavenCentral()
}


android {
    compileSdkVersion 29
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }
}

kotlin {
    android()

    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
    }

    js(LEGACY) {
        browser {
            binaries.executable()
            webpackTask {
                cssSupport.enabled = true
            }
            runTask {
                cssSupport.enabled = true
            }
            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }

    /* Targets configuration omitted. 
    *  To find out how to configure the targets, please follow the link:
    *  https://kotlinlang.org/docs/reference/building-mpp-with-gradle.html#setting-up-targets */

    sourceSets {
        commonMain {
            dependencies {
                compileOnly project(":room-annotations")
                implementation kotlin('stdlib-common')
                implementation "io.ktor:ktor-client-core:$version_ktor"
                implementation "io.ktor:ktor-client-json:$version_ktor"
                implementation "com.github.aakira:napier:$version_napier"
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'
                implementation "org.kodein.di:kodein-di:$version_kodein_di"

            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        roomAnnotations {

        }

        commonJvmMain {
            dependencies {
                compileOnly "org.nanohttpd:nanohttpd:$version_nanohttpd"
                compileOnly "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"
            }
        }

        androidMain {
            dependsOn commonJvmMain
            dependencies {
                implementation "androidx.room:room-runtime:$version_android_room"
                implementation "androidx.room:room-ktx:$version_android_room"
                implementation "androidx.lifecycle:lifecycle-livedata-ktx:$version_androidx_lifecycle"
                implementation "androidx.paging:paging-runtime:$version_androidx_paging"
                implementation "androidx.core:core-ktx:$version_androidx_core"
            }
        }

        androidTest {

        }


        jvmMain {
            dependsOn roomAnnotations
            dependsOn commonJvmMain
            dependencies {
                implementation project(":room-annotations")
                implementation "org.kodein.di:kodein-di-framework-ktor-server-jvm:$version_kodein_di"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "io.ktor:ktor-server-core:$version_ktor"
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')

                implementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$version_kotlin_mockito"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "com.squareup.okhttp3:mockwebserver:$version_mockwebserver"

                implementation "io.ktor:ktor-server-core:$version_ktor"
                implementation "io.ktor:ktor-server-netty:$version_ktor"
                implementation "io.ktor:ktor-jackson:$version_ktor"
                implementation "io.ktor:ktor-gson:$version_ktor"
                implementation "io.ktor:ktor-client-okhttp:$version_ktor"
                implementation "io.ktor:ktor-client-gson:$version_ktor"
            }
        }

        jsMain {
            dependsOn roomAnnotations
        }

        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "door"
            url = uri("https://maven.pkg.github.com/UstadMobile/door")
            credentials {
                username = project.hasProperty("user") ? project.getProperty("user"): ""
                password = project.hasProperty("key") ? project.getProperty("key"): ""
            }
        }
    }
}